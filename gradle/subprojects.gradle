subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'idea'
    apply plugin: 'spring-boot'
    apply from: "${rootDir}/gradle/checkstyle.gradle"
    apply from: "${rootDir}/gradle/findbugs.gradle"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    sourceSets {
        main {
            java {
                srcDirs = []
            }
            groovy {
                srcDirs += ['src/main/groovy', 'src/main/java']
            }
        }

        test {
            java {
                srcDirs = []
            }
            groovy {
                srcDirs += ['src/test/groovy', 'src/test/java']
            }
        }
    }

    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        compile libs.boot.starter
        compile libs.boot.devTools
        compile libs.groovy

        testCompile libs.boot.test.starter
    }

    dependencyManagement {
        imports {
            mavenBom libs.cloud.dependencies
        }
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled false
            html.enabled true
        }
    }
    bootRun {
        addResources = true
    }
}