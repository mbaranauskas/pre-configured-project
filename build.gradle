buildscript {
    ext {
        springBootVersion = '1.4.0.M3'
    }

    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'gradle.plugin.com.palantir.gradle.gitversion:gradle-git-version:0.5.2'
    }
}

apply from: 'gradle/libs.gradle'
apply from: 'gradle/subprojects.gradle'

ext.runnableProjects = [project(':gateway'),
                        project(':service-discovery'),
                        project(':project-a:web')]

configure(runnableProjects) {
    apply from: "${rootDir}/gradle/version-resolver.gradle"

    springBoot {
        bootRepackage {
            withJarTask = jar
            executable = true
        }
    }
}
configure(subprojects - runnableProjects) {
    springBoot {
        bootRepackage {
            enabled = false
            executable = false
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}